Use cWebForm.pkg

Struct tPasswordStrengthCharsets
    { Name="number" }
    Boolean hasNumber
    { Name="lower" }
    Boolean hasLower
    { Name="upper" }
    Boolean hasUpper
    { Name="punctuation" }
    Boolean hasPunctuation
    { Name="symbol" }
    Boolean hasSymbol
    String other
End_Struct

Struct tPasswordStrengthStatistics
    Number charsetSize
    Boolean commonPassword
    Number nistEntropyBits
    Number passwordLength
    Number shannonEntropyBits
    String strengthCode
    Number trigraphEntropyBits
    tPasswordStrengthCharsets charsets
End_Struct

Class cWebNewPasswordForm is a cWebForm
    
    Procedure Construct_Object
        Forward Send Construct_Object
        Set psJSClass to "WebNewPasswordForm"
        Set pbPassword to True

        // Add web properties here
        { WebProperty=Client }
        Property Boolean pbServerOnInputStrength False
        { WebProperty=Client }
        Property String psClientOnInputStrength ""
        { WebProperty=Client }
        Property Boolean pbServerOnInputDetails False
        { WebProperty=Client }
        Property String psClientOnInputDetails ""
        { WebProperty=Client }
        Property String psMeterColorFrom "ff4047"
        { WebProperty=Client }
        Property String psMeterColorTo "00ff6e"
    End_Procedure

    Procedure End_Construct_Object
        Forward Send End_Construct_Object

        // Publish event methods here (and other methods you want to call from the client)
        WebPublishProcedure OnInputStrength
        WebPublishProcedure OnInputDetailsProxy
    End_Procedure
    
    { Visibility=Private }
    Procedure OnInputDetailsProxy
        Handle hoJson
        tPasswordStrengthStatistics stData
        
        Get phoActionJsonData to hoJson
        Set pbRequireAllMembers of hoJson to False
        Get JsonToDataType of hoJson to stData
        Send OnInputDetails stData
    End_Procedure
    
    { MethodType=Event }
    Procedure OnInputStrength Integer iStrength
    End_Procedure
    
    { MethodType=Event }
    Procedure OnInputDetails tPasswordStrengthStatistics stData
    End_Procedure

End_Class